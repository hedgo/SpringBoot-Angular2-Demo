buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }

    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.moowork.gradle:gradle-node-plugin:0.13"
        classpath "com.moowork.gradle:gradle-gulp-plugin:0.13"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'

//Angular2のビルドに必要なプラグイン
apply plugin: "com.moowork.node"

node {
    version = '6.7.0' //使用するnodeのバージョン
    download = true //各自の環境にダウンロードする
}

//npm run buildするタスク
//事前にnpmをインストールさせるならdependsOn: 'npm_install'入れておくと良い
task npmBuild(type: NpmTask) {
    args = ['run', 'build']
}

// gradleのbuildタスク実行時に`npm run build`する
build.dependsOn npmBuild

jar {
    baseName = 'angular2-spring-boot-demo'
    version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-web')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}
